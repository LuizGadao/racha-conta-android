buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.github.triplet.play'
apply plugin: 'de.felixschulze.gradle.hockeyapp'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

apply from: "${rootProject.getRootDir()}/config/quality/quality.gradle"
apply from: "${rootProject.getRootDir()}/jacoco.gradle"

// Versioning information
def versionMajor = 0
def versionMinor = 3
def versionPatch = 8

android {

    compileSdkVersion 23
    buildToolsVersion '25.0.2'

    defaultConfig {

        applicationId "com.orogersilva.rachaconta.taberna"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch * 1
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {

        release {

            storeFile rootProject.file('android.jks')
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASSWORD")
        }
    }

    buildTypes {

        debug{
            ext.betaDistributionGroupAliasesFilePath="${rootProject.getRootDir()}/crashlytics/group_aliases.txt"
            ext.betaDistributionReleaseNotesFilePath="${rootProject.getRootDir()}/crashlytics/release_notes.txt"
        }

        release {

            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testBuildType obtainTestBuildType()

    play {

        serviceAccountEmail = System.getenv("SERVICE_ACCOUNT_EMAIL")
        pk12File = rootProject.file('play.p12')
        track = 'alpha'
    }
}

hockeyapp {

    apiToken = System.getenv("HOCKEY_APP_API_TOKEN")
    notify = 1
    teams = 72599
}

// Show test status in console.
tasks.withType(Test) {

    testLogging {
        events "passed", "failed"
    }
}

def obtainTestBuildType() {

    def result = "debug"

    if (project.hasProperty("testBuildType")) {
        result = project.getProperties().get("testBuildType")
    }

    return result
}

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "org.robolectric:robolectric:3.1.4"

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support:support-annotations:23.1.1'
}
